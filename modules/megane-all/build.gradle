plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def stdout = new ByteArrayOutputStream()
exec {
    executable = "git"
    args = ["describe", "--tags", "--always", "--first-parent"]
    standardOutput = stdout
}
def props = new Properties()
file("../../gradle.properties").withInputStream { props.load(it) }

group = "com.github.badasintended"
archivesBaseName = "megane-all"

def baseVersion = props.getProperty("mod")
version = "$baseVersion-${stdout.toString().trim()}"

dependencies {
    minecraft "com.mojang:minecraft:${props.getProperty("minecraft")}"
    mappings "net.fabricmc:yarn:${props.getProperty("yarn")}:v2"
    modImplementation "net.fabricmc:fabric-loader:${props.getProperty("loader")}"

    include files("../../out/base/megane-${baseVersion}.jar")

    file("../../out/modules").eachDir { dir ->
        dir.eachFileMatch(~/.*-0\+[a-f0-9]*\.jar$/) {
            include files(it.path)
        }
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "../../LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        mavenLocal()
    }
}

